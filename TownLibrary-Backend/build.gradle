plugins {
	id 'org.springframework.boot' version '2.5.5'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'jacoco'	// for code coverage

	// This allows the task jacocoTestReport to report all coverage ratios
	// supported by jacoco (it will actually print it out)
	id 'org.barfuin.gradle.jacocolog' version '2.0.0'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	runtimeOnly 'org.postgresql:postgresql'
	testImplementation 'org.mockito:mockito-core:2.+'
	testImplementation 'io.rest-assured:spring-mock-mvc:4.4.0' // for integration tests in junit
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.junit.jupiter:junit-jupiter-params' // for parameterized tests

	// Stuff specific to the local profile -- specified by
	// application-local.properties -- such as:
	// *  using a local in-memory database instead of heroku.
	//
	// Can be launched by using your favorite IDE or by adding
	// `SPRING_PROFILES_ACTIVE=local` before the gradle command:
	// `SPRING_PROFILES_ACTIVE=local ./gradlew test` for example.
	runtimeOnly 'com.h2database:h2:1.4.200'
}

// Note that integration tests must be tagged "integration"
// (based on https://stackoverflow.com/questions/40891867/junit5-tag-specific-gradle-task)

task jacocoUnitTestReport(type: JacocoReport) {
	reports { html.required = true }
	afterEvaluate {
		executionData(file("$buildDir/jacoco/unitTest.exec"))
		classDirectories.setFrom(files("$buildDir/classes/java/main"))
		sourceDirectories.setFrom(project.sourceSets.main.java.srcDirs)
	}
}

// Runs all tests not-tagged "integration"
task unitTest(type: Test) {
	// defaults to testing with ci profile
	systemProperty "spring.profiles.active", (System.getenv("SPRING_PROFILES_ACTIVE") ?: "ci")

	useJUnitPlatform { excludeTags "integration" }
	finalizedBy jacocoUnitTestReport
}

task jacocoIntegrationTestReport(type: JacocoReport) {
	reports { html.required = true }
	afterEvaluate {
		executionData(file("$buildDir/jacoco/integrationTest.exec"))
		classDirectories.setFrom(files("$buildDir/classes/java/main"))
		sourceDirectories.setFrom(project.sourceSets.main.java.srcDirs)
	}
}

// Runs only the tests tagged "integration"
task integrationTest(type: Test) {
	// defaults to testing with ci profile
	systemProperty "spring.profiles.active", (System.getenv("SPRING_PROFILES_ACTIVE") ?: "ci")

	useJUnitPlatform { includeTags "integration" }
	finalizedBy jacocoIntegrationTestReport
}

// Runs all the tests regardless of tag
test {
	// defaults to testing with ci profile
	systemProperty "spring.profiles.active", (System.getenv("SPRING_PROFILES_ACTIVE") ?: "ci")

	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

// and test coverage verification is based on the result of test
jacocoTestCoverageVerification {
	violationRules {
		rule {
			limit {
				minimum = 0.6	// instruction coverage >= 0.6
			}
		}
	}
}